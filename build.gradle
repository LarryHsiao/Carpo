plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.2.61'
    id 'org.sonarqube' version '2.6.2'
    id "no.tornado.fxlauncher" version "1.0.20"
}

/**
 * Obtain current tag on git log tree.
 */
def getCurrentGitTag = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'tag', '--points-at', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString()
}

def getCurrentDate = {
    def date = new Date()
    def formattedDate = date.format('yyyyMMddHHmm')
    return formattedDate
}

def getVersionName = { ->
    def tag = getCurrentGitTag()
    if (tag.empty) {
        return "Dev-${getCurrentDate()}"
    } else {
        return tag
    }
}

apply plugin: 'application'
mainClassName = "com.silverhetch.carpo.Main"

sourceSets {
    main {
        java {
            // we put the java classes in kotlin source tree
            srcDirs = [
                    'src/main/kotlin',
                    'build/generated/com/silverhetch/carpo/'
            ]
        }
    }
}

group 'com.silverhetch'
version getVersionName()

repositories {
    mavenCentral()
    maven { url = 'http://172.104.79.181:81/repository/Elizabeth/' }
}

dependencies {
    testCompile "junit:junit:4.12"
    testCompile "org.testfx:testfx-core:4.0.14-alpha"
    testCompile "org.testfx:testfx-junit:4.0.14-alpha"
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    compile "com.silverhetch:clotho:1.1.0"
    compile 'de.codecentric.centerdevice:javafxsvg:1.3.0'
    compile 'com.jfoenix:jfoenix:8.0.7'
    compile 'org.xerial:sqlite-jdbc:3.15.1'
    compile 'org.nanohttpd:nanohttpd-webserver:2.3.1'
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

task unitTest(type: Test) {
    exclude '**/javafx/**'
}

task uiTest(type: Test) {
    include '**/javafx/**'
}

apply plugin: 'jacoco'
jacocoTestReport {
    reports {
        xml.enabled = true
    }
}

apply plugin: 'org.sonarqube'
sonarqube {
    properties {
        property "sonar.projectVersion", "${version}"

        property 'sonar.sources', 'src/main/kotlin'
        property "sonar.tests", "src/test/kotlin"

        property "sonar.java.binaries", "${project.buildDir}/classes/kotlin/main"
        property "sonar.java.test.binaries", "${project.buildDir}/classes/kotlin/test"

        property "sonar.jacoco.reportPaths", "${project.buildDir}/jacoco/test.exec"
        property "sonar.junit.reportPaths", "${project.buildDir}/test-results/test"
    }
}

fxlauncher {
    applicationVendor 'SilverHetch'
    applicationUrl 'https://silverhetch.com:12000'
    applicationMainClass 'com.silverhetch.carpo.Main'
    acceptDowngrade false
    deployTarget 'larryhsiao@silverhetch.com:CarpoDeployment'
}


task generateConsts {
    inputs.property "version", project.version
    outputs.dir "$buildDir/generated"
    doFirst {
        def versionFile = file("$buildDir/generated/com/silverhetch/carpo/Consts.java")

        versionFile.parentFile.mkdirs()
        versionFile.text =
                """
package com.silverhetch.carpo;

public class Consts {
    public static final String VERSION = "$project.version";
}
"""
    }
}
compileKotlin.dependsOn generateConsts