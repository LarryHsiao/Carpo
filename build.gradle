buildscript {
    repositories {
        maven { url = 'http://172.104.79.181:81/repository/Elizabeth/' }
    }
}

plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.2.61'
    id 'org.sonarqube' version '2.6.2'
    id "no.tornado.fxlauncher" version "1.0.20"
    id 'de.fuerstenau.buildconfig' version '1.1.8'
    id 'com.silverhetch.thea' version '1.2.1'
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.8'
}

apply plugin: 'idea'
mainClassName = "com.silverhetch.carpo.Main"

println(thea.versionIndicator.apply("")) // print current version info

group 'com.silverhetch'
version thea.version.versionByFlavor("").versionName()

javafx {
    version = "11.0.2"
    modules = ['javafx.controls', 'javafx.fxml']
}

repositories {
    mavenCentral()
    maven { url = 'http://172.104.79.181:81/repository/Elizabeth/' }
}

dependencies {
    testCompile "junit:junit:4.12"
    testCompile "org.testfx:testfx-core:4.0.14-alpha"
    testCompile "org.testfx:testfx-junit:4.0.14-alpha"
    compile 'com.sun.activation:javax.activation:1.2.0'
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    compile "com.silverhetch:clotho:1.1.0"
    compile 'com.jfoenix:jfoenix:9.0.8'
    compile 'org.xerial:sqlite-jdbc:3.15.1'
    compile 'org.nanohttpd:nanohttpd-webserver:2.3.1'
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

sourceSets {
    main {
        java {
            srcDir 'src/main/kotlin'
        }
    }
}

task unitTest(type: Test) {
    exclude '**/javafx/**'
}

task uiTest(type: Test) {
    include '**/javafx/**'
}

apply plugin: 'jacoco'
jacoco {
    toolVersion = "0.8.2"
}
jacocoTestReport {
    reports {
        xml.enabled = true
    }
}

apply plugin: 'org.sonarqube'
sonarqube {
    properties {
        property "sonar.projectVersion", "${version}"

        property 'sonar.sources', 'src/main/kotlin'
        property "sonar.tests", "src/test/kotlin"

        property "sonar.java.binaries", "${project.buildDir}/classes/kotlin/main"
        property "sonar.java.test.binaries", "${project.buildDir}/classes/kotlin/test"

        property "sonar.jacoco.reportPaths", "${project.buildDir}/jacoco/test.exec"
        property "sonar.junit.reportPaths", "${project.buildDir}/test-results/test"
    }
}

fxlauncher {
    applicationVendor 'SilverHetch'
    applicationUrl 'https://silverhetch.com:12000'
    applicationMainClass 'com.silverhetch.carpo.Main'
    acceptDowngrade false
    deployTarget 'larryhsiao@silverhetch.com:CarpoDeployment'
}
